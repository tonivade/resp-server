plugins {
  id 'java-library'
  id 'jacoco'
  id 'maven-publish'
  id 'signing'
  id 'io.github.gradle-nexus.publish-plugin' version "$nexusVersion"
  id 'com.github.ben-manes.versions' version "$versionsVersion"
}

group = 'com.github.tonivade'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

java {
  withJavadocJar()
  withSourcesJar()
}

jacoco {
  toolVersion = jacocoVersion
}

test {
  useJUnitPlatform()

  jacoco {
    destinationFile = file("${buildDir}/jacoco/test.exec")
  }
}

jacocoTestReport {
  reports {
    xml.required = true
    html.required = true
    xml.outputLocation = file("${buildDir}/reports/jacoco/report.xml")
  }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = 'https://oss.sonatype.org/content/repositories/snapshots'
  }
}

dependencies {
  api "io.netty:netty-buffer:$nettyVersion"
  api "io.netty:netty-common:$nettyVersion"
  api "io.netty:netty-codec:$nettyVersion"
  api "io.netty:netty-handler:$nettyVersion"
  api "io.netty:netty-resolver:$nettyVersion"
  api "io.netty:netty-transport:$nettyVersion"
  api "io.netty:netty-transport-classes-epoll:$nettyVersion"
  api "io.reactivex.rxjava3:rxjava:$rxjavaVersion"
  api "org.slf4j:slf4j-api:$slf4jVersion"
  implementation "io.netty:netty-transport-native-epoll:$nettyVersion:linux-aarch_64"
  implementation "io.netty:netty-transport-native-epoll:$nettyVersion:linux-x86_64"
  testImplementation "junit:junit:$junitVersion"
  testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitJupiterVersion"
  testImplementation "org.mockito:mockito-core:$mockitoVersion"
  testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
  testRuntimeOnly "ch.qos.logback:logback-classic:$logbackVersion"
}

publishing {
  publications {
    mavenJava(MavenPublication) {

      from components.java

      pom {
        name = 'RESP Server'
        description = 'Netty implementation of REdis Serialization Protocol, and a simple framework to implement command based protocols'
        url = 'https://github.com/tonivade/resp-server'
        inceptionYear = '2015'

        scm {
          url = 'https://github.com/tonivade/resp-server'
          connection = 'scm:https://github.com/tonivade/resp-server.git'
          developerConnection = 'scm:git://github.com/tonivade/resp-server.git'
        }

        licenses {
          license {
            name = 'The MIT License (MIT)'
            url = 'https://github.com/tonivade/resp-server/blob/master/LICENSE'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'tonivade'
            name = 'Antonio Mu√±oz'
            email = 'antoniogmc@gmail.com'
          }
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}

nexusPublishing {
  repositories {
    sonatype {

      stagingProfileId = '19a96b937cdb5'

      username = project.findProperty('nexusUsername')
      password = project.findProperty('nexusPassword')
    }
  }

  clientTimeout = Duration.ofSeconds(300)
  connectTimeout = Duration.ofSeconds(60)

  transitionCheckOptions {
    maxRetries.set(40)
    delayBetween.set(java.time.Duration.ofMillis(3000))
  }
}
